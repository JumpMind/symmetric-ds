buildscript {
    repositories {
        jcenter()
        maven { url 'https://repo.spring.io/plugins-release' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.7'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'io.spring.gradle:docbook-reference-plugin:0.3.1'
        classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0'
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.1'
        classpath 'co.riiid:gradle-github-plugin:0.4.2'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.docker-remote-api'
apply plugin: 'co.riiid.gradle'

task develop {
    dependsOn tasks.cleanEclipse, tasks.eclipse, tasks.cleanIdea, tasks.idea
}

github {
    owner = 'JumpMind'
    repo = 'symmetric-ds'
    token = githubToken
    tagName = version
    targetCommitish = scmBranch.substring(scmBranch.lastIndexOf('/')+1)
    name = version
    prerelease = false
}

idea {
    project {
        ipr {
            //you can tinker with the output *.ipr file before it's written out
            withXml {
                // turn on annotation processing
                def node = it.asNode().children()[0].get('annotationProcessing')[0];
                node.attributes().clear();
                node.appendNode('profile',['default':'true', 'name':'Default', 'enabled':'true'])

            }
        }
    }
}

task buildScriptDependencies(type: org.gradle.api.tasks.diagnostics.DependencyReportTask) {
    configurations = project.buildscript.configurations
}

if (project.properties.containsKey('pro')) {
    install.enabled = false
    uploadArchives.enabled = false
} else {
    apply from: symAssembleDir + '/common.gradle'
}

task generateJavadoc(type: Javadoc) {
    destinationDir = new File(buildDir, 'doc/javadoc')
    title = "SymmetricDS API $version"
	group = 'SymmetricDS'
	description = 'Generate JavaDoc HTML pages for API documentation'
}

task buildSymmetric {
	group = 'SymmetricDS'
	description = 'Build software, documentation, and distribution ZIP files'
    dependsOn ':symmetric-android:androidDistZip', ':symmetric-server:serverDistZip'
}

task publishSymmetric {
    dependsOn buildSymmetric
	group = 'SymmetricDS'
	description = 'Publish software to SourceForge website'
    doLast {
        ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
        classpath: configurations.sshAntTask.asPath)

        println "Publishing android distribution to source forge"
        ant.scp(
            todir: "$sourceforgeUser:$sourceforgePassword@frs.sourceforge.net:/home/frs/project/symmetricds/symmetricds/symmetricds-$majorMinorVersion",
            trust: 'true',
            verbose: 'true') {
            fileset(dir: "../symmetric-android/build/distributions/") {
                include(name: 'symmetric-android-' + version + '.zip')
            }
        }

        println "Publishing server distribution to source forge"
        ant.scp(
            todir: "$sourceforgeUser:$sourceforgePassword@frs.sourceforge.net:/home/frs/project/symmetricds/symmetricds/symmetricds-$majorMinorVersion",
            trust: 'true',
            verbose: 'true') {
            fileset(dir: "../symmetric-server/build/distributions/") { 
                include(name: 'symmetric-server-' + version + '.zip')
            }
        }
    }
}

task releaseSymmetric {
    dependsOn publishSymmetric
    dependsOn task('symmetric-server:publishDoc') 
	group = 'SymmetricDS'
	description = 'Publish software to SourceForge website and online documentation to SymmetricDS website'
}

docker {
    registryCredentials {
        username = "$dockerUser".toString()
        password = "$dockerPassword".toString()
    }
}

task copyDockerReadme(type: Copy) {
    from "Dockerfile.md"
    into "../symmetric-server/build/distributions/README.md"
}

task copyDockerfile(type: Copy) {
    dependsOn copyDockerReadme
    from "Dockerfile"
    into "../symmetric-server/build/distributions"
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
    dependsOn copyDockerfile
    inputDir = file("../symmetric-server/build/distributions")
    tags = ["jumpmind/symmetricds:latest", "jumpmind/symmetricds:${version}".toString()]
    buildArgs = ["SERVER_ZIP":"symmetric-server-${version}.zip"]
}

task publishDockerLatest(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    dependsOn buildImage
    imageName = "jumpmind/symmetricds:latest"
}

task publishDocker(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage) {
    dependsOn publishDockerLatest
    imageName = "jumpmind/symmetricds:${version}".toString()
    doLast {
        println "Pushing Docker Image to the jumpmind/symmetricds Repository"
    }
}
