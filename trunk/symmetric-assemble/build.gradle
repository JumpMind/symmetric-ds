buildscript {
    repositories {
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.1'
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
    }
}

apply from: ossDir + '/common.gradle'

project(":symmetric-csv") {
    description = 'General purpose csv parser'
    dependencies {
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
    }
}

project(":symmetric-wrapper") {
    description = 'General purpose cross platform service wrapper'

    dependencies {
        compile "net.java.dev.jna:jna:$jnaVersion"
        compile "net.java.dev.jna:jna-platform:$jnaVersion"
        compile "bouncycastle:bcprov-jdk15:$bouncyCastleVersion"
        provided "org.codehaus.mojo:animal-sniffer-annotations:$animalSnifferVersion"
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
    }
}

project(':symmetric-util') {
    description = 'Utility classes that are used by several Symmetric libraries'

    dependencies {
        compile "commons-lang:commons-lang:$commonsLangVersion"
        compile "commons-io:commons-io:$commonsIoVersion"
        compile "commons-codec:commons-codec:$commonsCodecVersion"
        compile "commons-collections:commons-collections:$commonsCollectionVersion"
        compile "bouncycastle:bcprov-jdk15:$bouncyCastleVersion"
        provided "org.codehaus.mojo:animal-sniffer-annotations:$animalSnifferVersion"
    }
}

project(':symmetric-db') {
    description = 'Database classes that are used by several Symmetric libraries'

    dependencies {
        compile project(":symmetric-util")
        compile "net.sf.kxml:kxml2:2.3.0"
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
    }
}

project(":symmetric-jdbc") {
    description = 'Library that contains the jdbc implementation for symmetric-db'

    dependencies {
        compile project(":symmetric-db")
        compile "commons-beanutils:commons-beanutils:$commonsBeanUtilsVersion"
        compile "commons-dbcp:commons-dbcp:$commonsDbcpVersion"
        compile "org.springframework:spring-jdbc:$springVersion"
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
    }
}

project(':symmetric-io') {
    description = 'Data input/output libraries'

    dependencies {
        compile project(":symmetric-csv")
        compile project(":symmetric-db")
        compile "org.beanshell:bsh:$bshVersion"
        compile "net.sourceforge.jeval:jeval:0.9.4"
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
        testCompile project(":symmetric-jdbc")
        testCompile project(path: ':symmetric-jdbc', configuration: 'testArtifacts')
    }
}

project(':symmetric-core') {
    description = 'Central SymmetricDS library.  Excludes direct dependencies on JDBC.'

    dependencies {
        compile project(":symmetric-io")
        compile "commons-fileupload:commons-fileupload:$commonsFileuploadVersion"
        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-jdbc', configuration: 'testArtifacts')
    }

    jar {
        filesMatching("**/*.properties") {
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                'appVersion': version,
                'buildTime': new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new java.util.Date()),
                'scmVersion' : scmVersion
            ])
        }
    }
}

project(':symmetric-android') {
    description = 'SymmetricDS Client library for Android clients'

    dependencies {
        compile (project(":symmetric-core")) {
            exclude group:'org.slf4j'
            exclude group:'log4j'
            exclude group:'net.sf.kxml'
        }
        compile "org.slf4j:slf4j-android:1.6.1-RC1"
        provided "com.google.android:android:2.1.2"
    }

    apply plugin: 'distribution'

    distributions {
        android {
            baseName = 'android'
            contents {
                from { 'COPYING' }
                from { 'CREDITS' }
                from { 'NOTICE' }
                into('libs') {
                    from configurations.runtime - configurations.provided
                    from jar
                }
            }
        }
    }
}

project(':symmetric-client') {
    description = 'SymmetricDS Client library for JDBC clients'

    dependencies {
        compile project(":symmetric-core")
        compile project(":symmetric-jdbc")
        compile "org.springframework:spring-context:$springVersion"
        compile "commons-cli:commons-cli:$commonsCliVersion"
        compile "commons-vfs:commons-vfs:1.0"
        compile "com.jcraft:jsch:0.1.48"

        provided "org.mongodb:mongo-java-driver:2.12.3"
        provided "org.codehaus.mojo:animal-sniffer-annotations:$animalSnifferVersion"
        provided "com.amazonaws:aws-java-sdk:1.8.9.1"

        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-io', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-jdbc', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-core', configuration: 'testArtifacts')
    }
}

project(':symmetric-server') {
    description = 'SymmetricDS Server library for JDBC client'

    dependencies {
        compile project(":symmetric-client")
        compile "javax.mail:mail:1.4.5"
        compile "org.springframework:spring-jms:$springVersion"
        compile "org.springframework:spring-web:$springVersion"
        compile "javax.xml.bind:jaxb-api:2.2.7"
        compile "org.codehaus.jackson:jackson-mapper-asl:1.9.9"
        compile "org.jdom:jdom:1.1"
        compile "com.mangofactory:swagger-springmvc:0.6.5"

        provided project(":symmetric-wrapper")
        provided "javax.jms:jms-api:1.1-rev-1"
        provided "org.mongodb:mongo-java-driver:2.12.3"
        provided "org.codehaus.mojo:animal-sniffer-annotations:$animalSnifferVersion"
        provided "com.amazonaws:aws-java-sdk:1.8.9.1"
        provided "javax.servlet:servlet-api:2.5"
        provided "org.eclipse.jetty:jetty-server:$jettyVersion"
        provided "org.eclipse.jetty:jetty-servlets:$jettyVersion"
        provided "org.eclipse.jetty:jetty-webapp:$jettyVersion"
        provided "org.eclipse.jetty:jetty-jmx:$jettyVersion"
        provided "mx4j:mx4j-tools:3.0.1"

        testCompile project(path: ':symmetric-util', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-io', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-jdbc', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-core', configuration: 'testArtifacts')
        testCompile project(path: ':symmetric-client', configuration: 'testArtifacts')
    }

    apply plugin: 'distribution'

    distributions {
        server {
            baseName = 'server'
            contents {
                from { 'COPYING' }
                from { 'CREDITS' }
                from { 'NOTICE' }
                from ('src/main/deploy') {
                    exclude 'bin/sym'
                    exclude 'bin/symadmin'
                    exclude 'bin/dbexport'
                    exclude 'bin/dbimport'
                    exclude 'bin/dbfill'
                    exclude 'bin/jmx'
                    exclude 'bin/dbsql'
                    exclude 'bin/sym_service'
                    exclude 'bin/setenv'
                }
                from ('src/main/deploy') {
                    include 'bin/sym'
                    include 'bin/symadmin'
                    include 'bin/dbexport'
                    include 'bin/dbimport'
                    include 'bin/dbfill'
                    include 'bin/jmx'
                    include 'bin/dbsql'
                    include 'bin/sym_service'
                    include 'bin/setenv'
                    fileMode 755
                }
                from('../symmetric-core/src/main/resources/symmetric-default.properties') {
                    rename 'symmetric-default.properties', 'conf/symmetric.properties'
                    fileMode 664
                }
                into('lib') {
                    from configurations.provided
                    rename 'symmetric-wrapper(.*).jar', 'symmetric-wrapper.jar'
                }
                into('web/WEB-INF/lib') {
                    from configurations.runtime - configurations.provided
                    from jar
                }
            }
        }
    }
}


task wrapper(type: Wrapper) { gradleVersion = '2.2.1' }
