apply plugin: 'org.asciidoctor.gradle.asciidoctor'

configurations{ generateAppendixesDependencies }

dependencies {
    generateAppendixesDependencies project(":symmetric-util")
    generateAppendixesDependencies project(":symmetric-db")
}

task generateAppendixes {
    doLast{
        javaexec {
            main = 'org.jumpmind.properties.DefaultParameterParser'
            classpath = configurations.generateAppendixesDependencies
            args = [
                symAssembleDir + '/../symmetric-server/src/main/deploy/conf/symmetric-server.properties',
                symAssembleDir + '/src/asciidoc/generated/server-parameters.ad',
                'false',
                'asciidoc'
            ]
        }

        javaexec {
            main = 'org.jumpmind.properties.DefaultParameterParser'
            classpath = configurations.generateAppendixesDependencies
            args = [
                symAssembleDir + '/../symmetric-core/src/main/resources/symmetric-default.properties',
                symAssembleDir + '/src/asciidoc/generated/startup-parameters.ad',
                'false',
                'asciidoc'
            ]
        }

        javaexec {
            main = 'org.jumpmind.properties.DefaultParameterParser'
            classpath = configurations.generateAppendixesDependencies
            args = [
                symAssembleDir + '/../symmetric-core/src/main/resources/symmetric-default.properties',
                symAssembleDir + '/src/asciidoc/generated/runtime-parameters.ad',
                'true',
                'asciidoc'
            ]
        }

        javaexec {
            main = 'org.jumpmind.db.io.DatabaseXmlAsciiDocBuilder'
            classpath = configurations.generateAppendixesDependencies
            args = [
                symAssembleDir + '/../symmetric-core/src/main/resources/symmetric-schema.xml',
                symAssembleDir + '/src/asciidoc/generated/schema.ad'
            ]
        }
    }
}

task generateDocs(type: Copy) {
    
    dependsOn asciidoctor
    
    destinationDir = file("$buildDir/src/asciidoc")
    if (project.properties.containsKey('pro')) {
        destinationDir = file("../symmetric-pro/src/main/webapp/doc")
    }

    into('html') {
        from ("$buildDir/src/asciidoc/html5") { include "user-guide.html" }
    }
    
    into('html') {
        from ("$buildDir/src/asciidoc/html5/tutorials") { include "tutorials.html" }
    }
    
    into('html/images') {
        from ("$buildDir/src/asciidoc/html5/images") 
    }

    into('pdf') {
        from ("$buildDir/src/asciidoc/pdf") { include "user-guide.pdf" }
    }

    into('pdf') {
        from ("$buildDir/src/asciidoc/pdf/tutorials") { include "tutorials.pdf" }
    }

}

asciidoctor { (1)
    backends = docFormat.split ","
    sourceDir = file(symAssembleDir + '/src/asciidoc')
    outputDir = file("$buildDir/src/asciidoc")
    options = [
        doctype: 'book',
        sourceDocumentNames: [
            symAssembleDir + 'user-guide.ad',
            symAssembleDir + 'tutorials/tutorials.ad']
    ]    
    attributes 'source-highlighter':'coderay'
    if (project.properties.containsKey('pro')) {
       attributes['pro'] = 'true'  
    }     
}

asciidoctor.dependsOn { tasks.generateAppendixes }
